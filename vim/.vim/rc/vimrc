if &compatible
  set nocompatible
endif

function! s:source_rc(path, ...) abort "{{{
  let use_global = get(a:000, 0, !has('vim_starting'))
  let abspath = resolve(expand('~/.vim/rc/' . a:path . '.vim'))
  if !use_global
    execute 'source' fnameescape(abspath)
    return
  endif

  " substitute all 'set' to 'setglobal'
  let content = map(readfile(abspath),
        \ 'substitute(v:val, "^\\W*\\zsset\\ze\\W", "setglobal", "")')
  " create tempfile and source the tempfile
  let tempfile = tempname()
  try
    call writefile(content, tempfile)
    execute printf('source %s', fnameescape(tempfile))
  finally
    if filereadable(tempfile)
      call delete(tempfile)
    endif
  endtry
endfunction"}}}

" Set augroup.
augroup MyAutoCmd
  autocmd!
  " Perl6
  autocmd BufRead,BufNewfile *.p6     setf perl6
  " Perl5
  autocmd BufRead,BufNewfile *.p5     setf perl
  autocmd BufNewFile,BufRead *.psgi   set filetype=perl
  autocmd BufNewFile,BufRead *.t      set filetype=perl

  " incsearch
  autocmd CmdlineEnter [/\?] set hlsearch
  autocmd CmdlineLeave [/\?] set nohlsearch
augroup END

if has('vim_starting')
  call s:source_rc('init/init')
  call s:source_rc('dein/init')
endif

call s:source_rc('dein/dein')
call s:source_rc('init/basic')
"call s:source_rc('lightline')
"call s:source_rc('go')

if !has('vim_starting')
  call dein#call_hook('source')
  call dein#call_hook('post_source')

  syntax enable
  filetype on
  filetype plugin on
  filetype indent on
endif
